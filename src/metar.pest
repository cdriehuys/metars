METAR = { station ~ observation_time ~ auto_kw? ~ wind ~ visibility ~ clouds ~ temp_dew }

station = { (ASCII_ALPHA_UPPER | ASCII_DIGIT){4} }
observation_time = { ASCII_DIGIT{6} ~ "Z" }

auto_kw = { "AUTO" }

// Mark wind as "compound atomic" rule to prohibit whitespace between elements.
wind = ${ direction ~ wind_speed ~ "KT" }
wind_speed = { ASCII_DIGIT{2} }

visibility = ${ visibility_distance ~ "SM" }
visibility_distance = @{ ASCII_DIGIT+ ~ ("/" ~ ASCII_DIGIT+)? }

clouds = { clouds_clr }
clouds_clr = { "CLR" }

temp_dew = ${ temp_measurement ~ "/" ~ temp_measurement }
temp_measurement = @{ "M"? ~ ASCII_DIGIT{2} }

// Compass direction given as 3 digits.
direction = { ASCII_DIGIT{3} }

WHITESPACE = _{ " " }
